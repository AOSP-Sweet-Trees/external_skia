# Copyright 2023 Google Inc.
#
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("../../gn/skia.gni")

config("public_config") {
  defines = []
  include_dirs = [ "." ]
}

skia_component("window") {
  check_includes = false
  testonly = true  # For access to gpu_tool_utils. TODO: clean up
  public_configs = [ ":public_config" ]
  public = [
    "SkDisplayParams.h",
    "SkWindowContext.h",
  ]
  sources = [
    "SkDisplayParams.h",
    "SkRasterWindowContext.h",
    "SkWindowContext.cpp",
    "SkWindowContext.h",
  ]
  if (is_android) {
    sources += [ "android/SkWindowContextFactory_android.h" ]
  }

  if (skia_use_gl) {
    sources += [
      "SkGLWindowContext.cpp",
      "SkGLWindowContext.h",
    ]
    if (is_android) {
      sources += [ "android/SkGLWindowContext_android.cpp" ]
    }
  }
  if (skia_use_vulkan) {
    sources += [
      "SkVulkanWindowContext.cpp",
      "SkVulkanWindowContext.h",
    ]
    if (is_android) {
      sources += [ "android/SkVulkanWindowContext_android.cpp" ]
    }
  }
  configs = [ "../../:skia_private" ]
  deps = [
    "../..:gpu_tool_utils",
    "../..:skia",
  ]
}
